#include <Arduino.h>
#include <NimBLEDevice.h>
#include "esp_system.h"
#include <esp_mac.h> 
#include <WiFi.h>
#include <esp_now.h>
#include "ContractsInclude.hpp"

NimBLEScan* pBLEScan = nullptr;

using namespace Contracts;

static TpmsPacket pendingMessage;

void onEspNowSent(const wifi_tx_info_t* tx_info, esp_now_send_status_t status) {
    Serial.print("ESP-NOW send status: ");
    Serial.println(status == ESP_NOW_SEND_SUCCESS ? "SUCCESS" : "FAIL");
}

class MyScanCallbacks : public NimBLEScanCallbacks {
    void onResult(const NimBLEAdvertisedDevice* advertisedDevice) override {
        String name = advertisedDevice->getName().c_str();

        // Ignore unnamed devices
        if (!name.length()) {
            return;
        }

        // Filter: only devices whose name starts with "TPMS"
        if (!name.startsWith("TPMS")) {
            return;
        }

        Serial.print(name);
        Serial.print("  |  ");
        Serial.print(advertisedDevice->getAddress().toString().c_str());
        Serial.print("  |  ");
        Serial.print(advertisedDevice->getAddress().getType());

        const std::vector<uint8_t>& payload = advertisedDevice->getPayload();
        size_t length = payload.size();

        if (length >= 32) {
            uint32_t pressure = 0;
            pressure |= (uint32_t)payload[17];
            pressure |= (uint32_t)payload[18] << 8;
            pressure |= (uint32_t)payload[19] << 16;
            pressure |= (uint32_t)payload[20] << 24;

            uint16_t temp = 0;
            temp |= (uint16_t)payload[21];
            temp |= (uint16_t)payload[22] << 8;

            uint32_t sensorId = 0;
            sensorId |= (uint32_t)payload[14];
            sensorId |= (uint32_t)payload[15] << 8;
            sensorId |= (uint32_t)payload[16] << 16;

            Serial.print("  |  Sensor: ");
            Serial.print(sensorId);
            Serial.print("  |  Pressure: ");
            Serial.print(pressure);
            Serial.print("  |  Temp: ");
            Serial.print(temp);

            pendingMessage.type = TYPE_TPMS;
            pendingMessage.sensorId = sensorId;
            pendingMessage.pressure = pressure;
            pendingMessage.temp     = temp;
            pendingMessage.sequence++;

            esp_err_t res = esp_now_send(S3_5_1_MAC, (uint8_t*)&pendingMessage, sizeof(pendingMessage));
            Serial.print("  |  ESP-NOW send: ");
            Serial.println(res == ESP_OK ? "OK" : String("ERR ") + res);

            const uint8_t len = (uint8_t)(1 + sizeof(TpmsPacket));
            Serial2.write(SYNC1);
            Serial2.write(SYNC2);
            Serial2.write(len);
            Serial2.write(TYPE_TPMS);
            Serial2.write((const uint8_t*)&pendingMessage, sizeof(TpmsPacket));

        } else {
            Serial.print("  |  Payload too short (");
            Serial.print(length);
            Serial.print(")");
        }

        Serial.println();
    }

    void onScanEnd(const NimBLEScanResults& results, int reason) override {
        Serial.println("Scan ended");
    }
};

// ==== Setup ==== //
void setup() {
    Serial.begin(115200);
    Serial.println();
    Serial.println("TPMS gateway: scanning TPMS* and forwarding via ESP-NOW.");

    // Binary link to other ESP over UART2 on GPIO16/17
    Serial2.begin(115200, SERIAL_8N1, 16, 17);

    //Init the sequence to 1 so that the Monitor can detect the first packet
    pendingMessage.sequence = 1;

    WiFi.mode(WIFI_STA);        // required for ESP-NOW
    WiFi.disconnect();          // just to be safe

    // Read the real STA MAC from efuse
    uint8_t staMac[6];
    esp_read_mac(staMac, ESP_MAC_WIFI_STA);

    char macStr[18];
    snprintf(macStr, sizeof(macStr),
            "%02X:%02X:%02X:%02X:%02X:%02X",
            staMac[0], staMac[1], staMac[2],
            staMac[3], staMac[4], staMac[5]);

    Serial.print("ESP-NOW (STA) MAC: ");
    Serial.println(macStr);

    // Now init ESP-NOW
    if (esp_now_init() != ESP_OK) {
        Serial.println("ESP-NOW init failed!");
        return;
    }

    // New-style callback signature for your core:
    esp_now_register_send_cb(onEspNowSent);

    // Add peer (your ESP32-S3)
    esp_now_peer_info_t peerInfo = {};
    memcpy(peerInfo.peer_addr, S3_5_1_MAC, 6);
    peerInfo.channel = 0;   // 0 = current Wi-Fi channel
    peerInfo.encrypt = false;

    if (esp_now_add_peer(&peerInfo) != ESP_OK) {
        Serial.println("Failed to add ESP-NOW peer!");
        while (true) { delay(1000); }
    }

    // --- BLE scanner init (NimBLE) --- //
    NimBLEDevice::init("TPMS-Gateway");
    NimBLEDevice::setPower(ESP_PWR_LVL_P9); // TX power for scan requests, etc.

    pBLEScan = NimBLEDevice::getScan();
    pBLEScan->setScanCallbacks(new MyScanCallbacks(), /*wantDuplicates=*/true);
    pBLEScan->setFilterPolicy(BLE_HCI_SCAN_FILT_NO_WL);
    pBLEScan->setActiveScan(true);  // request scan response
    
    Serial.println("Setup complete.");
}

void loop() {
    static uint32_t lastScanStart = 0;
    uint32_t now = millis();

    // If not currently scanning, start a 30-second scan every ~5s gap
    if (!pBLEScan->isScanning() && (now - lastScanStart > 5000)) {
        Serial.println("Starting TPMS scan...");
        // duration=30 (seconds), isContinue=false, restart=false
        pBLEScan->start(120000, false, false);
        lastScanStart = now;
    }

    delay(5000);

    //PING - send last received measurement
    esp_err_t res = esp_now_send(S3_5_1_MAC, (uint8_t*)&pendingMessage, sizeof(pendingMessage));
}
